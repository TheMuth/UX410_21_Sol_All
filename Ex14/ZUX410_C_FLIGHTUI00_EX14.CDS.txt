@AbapCatalog.sqlViewName: 'ZUX410FLTUI00_14'
@AbapCatalog.compiler.compareFilter: true
@AccessControl.authorizationCheck: #CHECK
@EndUserText.label: 'UX410 Flight UI'
@OData.publish: true

@UI.headerInfo.typeName : 'Flight'
@UI.headerInfo.typeNamePlural : 'Flights'

@Search.searchable: true

define view ZUX410_C_FLIGHTUI00_EX14
  as select from sflight as sf
  association to scarr    as sc  on  sf.carrid = sc.carrid
  association to sflights as sfs on  sfs.carrid = sf.carrid
                                 and sfs.connid = sf.connid
                                 and sfs.fldate = sf.fldate
{
      @UI.lineItem.position:10
  key sf.carrid,
      @UI.lineItem.position:20
  key sf.connid,
      @UI.lineItem.position:30
  key sf.fldate,
      @Search.defaultSearchElement: true
      @Search.fuzzinessThreshold: 0.7
      @UI.selectionField.position:10
      @UI.lineItem.position:40
      sc.carrname,
      @UI.selectionField.position:20  
      @UI.lineItem.position:70
      sfs.cityfrom,
      @UI.selectionField.position:30 
      @UI.lineItem.position:80
      sfs.cityto,
      @UI.lineItem.position:50
      @UI.lineItem.importance: #HIGH
      sf.planetype,
      @UI.lineItem.importance: #LOW
      @UI.lineItem.position:60
      sf.price,
      @Semantics.currencyCode: true
      sf.currency,
      @Semantics.amount.currencyCode: 'currency'
      @UI.lineItem.importance: #MEDIUM
      @UI.lineItem.position:90
      sf.price * sf.seatsocc as Total,
      sf.seatsmax,
      sf.seatsocc,
      sc.url,
      sf.paymentsum,
      sf.seatsmax_b,
      sf.seatsocc_b,
      sf.seatsmax_f,
      sf.seatsocc_f
}
